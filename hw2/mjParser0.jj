// This is supporting software for CS321 Compilers and Language Design I
// Copyright (c) Portland State University
//
//----------------------------------------------------------------------
// miniJava F14 Raw Grammar (JavaCC Specification)
//
// (For CS321 Fall 2014 - Jingke Li)
//
// Jong Seong Lee
// 11 / 06 / 14
// Homework 2: Parser
//
//----------------------------------------------------------------------
//
//

PARSER_BEGIN(mjParser0)
import java.io.*;

public class mjParser0 {
  public static void main(String [] args) {
    try {
      if (args.length == 1) {
	FileInputStream stream = new FileInputStream(args[0]);
	new mjParser0(stream).Program();
	stream.close();
	System.out.println("Program's syntax is valid.");
      } else {
	System.out.println("Need a file name as command-line argument.");
      }
    } catch (TokenMgrError e) {
      System.err.println(e);
    } catch (Exception e) {
      System.err.println(e);
    }
  }
}
PARSER_END(mjParser0)

//
// LEXER SECTION ---------------------------------------------------------------
//

SKIP : /* White space */
{
  " " | "\t" | "\n" | "\r" | "\f"
}

SKIP : /* Comments -- multi-line form and error detection not included */
{
  <"//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")> 
}

TOKEN : /* Keywords */
{
  "class" | "extends" | "static" | "public" | "void" | "int" | "boolean"
| "new" | "this" | "if" | "else" | "while" | "return" | "main" | "String" 
| "true" | "false" | "System" | "out" | "println" 
}

TOKEN : /* Internal tokens */
{
  <#DIGIT:  ["0"-"9"]>
| <#LETTER: (["A"-"Z"]|["a"-"z"])>
}

TOKEN : /* Integer literals -- error detection not included */
{
  <INTLIT: (<DIGIT>)+> 
}

TOKEN : /* String literals  -- error detection not included */
{
  <STRLIT: ("\"" (~["\"","\n"])* "\"")>
}

TOKEN : /* Identifiers */
{
  <ID: <LETTER> (<LETTER>|<DIGIT>)*>
}

TOKEN : /* Operators and delimiters */
{
   "+" | "-" | "*" | "/" | "&&" | "||" | "!" 
|  "==" | "!=" | "<" | "<=" | ">" | ">="
|  "=" | ";" | "," | "." | "(" | ")" | "[" | "]" | "{" | "}"
}

//
// PARSER SECTION ---------------------------------------------------------------
//

// Program -> {ClassDecl}
//
void Program(): {}
{
  (ClassDecl())* <EOF>
}

// ClassDecl -> "class" <ID> ["extends" <ID>] "{" {VarDecl} {MethodDecl} "}"
//
void ClassDecl(): {}
{
  "class" <ID> ["extends" <ID>] "{" (VarDecl())* (MethodDecl())* "}"
}

// MethodDecl -> "public" MethodDecl2
// 
void MethodDecl(): {} 
{
  "public" MethodDecl2()
}

// MethodDecl2 -> ExtType <ID> "(" [Param {"," Param}] ")" "{" {VarDecl} {Stmt} "}"
//             |  "static" "void" "main" "(" "String" "[" "]" <ID> ")" "{" {VarDecl} {Stmt} "}" 
// 
void MethodDecl2(): {}
{
  ExtType() <ID> "(" [Param() ("," Param())*] ")" MethodDecl3()
| "static" "void" "main" "(" "String" "[" "]" <ID> ")" MethodDecl3()
}

void MethodDecl3(): {}
{
  "{" (LOOKAHEAD(2) VarDecl())* (Stmt())* "}"
}

// Param -> Type <ID> 
//
void Param(): {} 
{
  Type() <ID> 
}

// VarDecl -> Type <ID> ["=" InitExpr] ";"
//  
void VarDecl(): {}
{
  Type() <ID> ["=" InitExpr()] ";"
}

// ExtType -> Type | "void"
//
void ExtType(): {}
{
  Type() | "void"
}

// Type -> BasicType 
//      |  BasicType "[" "]" 
//      |  <ID>
//
void Type(): {}
{
  BasicType() ["[" "]"]
| <ID>
}

// BasicType -> "int" | "boolean"
//
void BasicType(): {}
{
  "int" | "boolean"
}

// Stmt -> "{" {Stmt} "}"
//      |  ExtId "(" [Args] ")" ";"
//      |  Lvalue "=" InitExpr ";"
//      |  "if" "(" Expr ")" Stmt ["else" Stmt]
//      |  "while" "(" Expr ")" Stmt
//      |  "System" "." "out" "." "println" "(" [PrintArg] ")" ";"
//      |  "return" [Expr] ";"
//
void Stmt(): {}
{
  "{" (Stmt())* "}"
| ExtId() Stmt2()
| "if" "(" Expr() ")" Stmt() [LOOKAHEAD(2) "else" Stmt()]
| "while" "(" Expr() ")" Stmt()
| "System" "." "out" "." "println" "(" [ PrintArg() ] ")" ";"  
| "return" [Expr()] ";"
}

void Stmt2(): {}
{
  "(" [Args()] ")" ";" // common prefix
| ["[" Expr() "]"] "=" InitExpr() ";" // common prefix
}

// Args -> Expr {"," Expr}
//
void Args(): {}
{
  Expr() ("," Expr())*
}

// PrintArg -> Expr | <STRLIT>
//
void PrintArg(): {}
{
  Expr() | <STRLIT>
}

// InitExpr -> "new" InitExpr2
//          |  Expr 
//
void InitExpr(): {}
{
  "new" InitExpr2()
| Expr()
}

// InitExpr2 -> BasicType() "[" <INTLIT> "]"
//           |  <ID> "(" ")"
//
void InitExpr2(): {}
{
  BasicType() "[" <INTLIT> "]"
| <ID> "(" ")"
}

// Expr -> UnOp Expr Expr2
//      |  "(" Expr ")" Expr2
//      |  ExtId Expr3
//      |  Literal Expr2
//
void Expr(): {}
{
  UnOp() Expr() Expr2()
| "(" Expr() ")" Expr2() 
| ExtId() Expr3()
| Literal() Expr2()
}

// Expr3 -> "(" [Args()] ")" Expr2()
//       |  ["[" Expr() "]"] Expr2()
//
void Expr3(): {}
{
  "(" [Args()] ")" Expr2()
| ["[" Expr() "]"] Expr2()
}

// Expr2 ->  BinOp Expr Expr2 | epsilon
//
void Expr2(): {}
{
  [LOOKAHEAD(2) BinOp() Expr() Expr2()]
}

// ExtId -> ["this" "."] <ID> {"." <ID>} 
//
void ExtId(): {}
{
  ["this" "."] <ID> ("." <ID>)*
}

// Literal -> <INTLIT> | "true" | "false"
//
void Literal(): {}
{
  <INTLIT> | "true" | "false"
}

// BinOp -> "+" | "-" | "*" | "/" | "&&" | "||" 
//       |  "==" | "!=" | "<" | "<=" | ">" | ">=" 
//
void BinOp(): {}
{
  "+"  | "-"  | "*" | "/"  | "&&" | "||" 
| "==" | "!=" | "<" | "<=" | ">"  | ">=" 
}

// UnOp -> "-" | "!"
//
void UnOp(): {}
{
  "-" | "!"
}